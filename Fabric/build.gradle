plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'ferritecore.loader-conventions'
}

repositories {
    maven {
        url 'https://maven.parchmentmc.org'
    }
}

dependencies {
    minecraft group: 'com.mojang', name: 'minecraft', version: minecraft_version
    mappings loom.layered() {
        officialMojangMappings()
        //parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    modImplementation group: 'net.fabricmc', name: 'fabric-loader', version: fabric_loader_version
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

def versionReplacements = [
    "version": mod_version
]

processResources {
    inputs.properties(versionReplacements)
    filesMatching("fabric.mod.json") {
        expand versionReplacements
    }
}

jar {
    filesMatching("*.mixin.json") {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

// For some reason this doesn't work when in the "convention" scripts...
def customGradle = project.file('custom.gradle');
if (customGradle.exists()) {
    apply from: customGradle;
}
